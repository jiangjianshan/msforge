diff -urN liboggz-1.0.2-orig/configure.ac liboggz-1.0.2/configure.ac
--- liboggz-1.0.2-orig/configure.ac	2010-04-29 09:44:08.000000000 +0800
+++ liboggz-1.0.2/configure.ac	2025-11-14 11:45:28.976515500 +0800
@@ -1,13 +1,14 @@
 # Process this file with autoconf to produce a configure script.
-AC_INIT
+AC_INIT([liboggz],[1.0.2])
 AC_CONFIG_SRCDIR([src/liboggz/oggz.c])
 
 AC_PREREQ(2.53)
 
+AC_CONFIG_MACRO_DIRS([m4])
 AC_CANONICAL_TARGET
 
-AM_INIT_AUTOMAKE(liboggz, 1.0.2)
-AM_CONFIG_HEADER(config.h)
+AM_INIT_AUTOMAKE([subdir-objects foreign])
+AC_CONFIG_HEADERS([config.h])
 
 ################################################################################
 # Set the shared versioning info, according to section 6.3 of the libtool info #
@@ -35,7 +36,7 @@
 AC_PROG_INSTALL
 AC_PROG_LN_S
 AC_PROG_MAKE_SET
-AC_PROG_LIBTOOL
+LT_INIT
 
 AC_C_CONST
 AC_C_BIGENDIAN
@@ -122,22 +123,9 @@
 fi
 
 dnl Checks for libraries.
-LIBS=""
 
-# check for getopt in a separate library
-HAVE_GETOPT=no
-AC_CHECK_LIB(getopt, getopt, HAVE_GETOPT="yes")
-if test "x$HAVE_GETOPT" = xyes ; then
-  GETOPT_LIBS="-lgetopt"
-  AC_SUBST(GETOPT_LIBS)
-fi
-
-# check for getopt_long in standard library
-HAVE_GETOPT_LONG=no
-AC_CHECK_FUNC(getopt_long, HAVE_GETOPT_LONG="yes")
-if test "x$HAVE_GETOPT_LONG" = xyes ; then
-  AC_DEFINE(HAVE_GETOPT_LONG, [], [Define to 1 if you have the 'getopt_long' function])
-fi
+# Check for getopt and getopt_long functions
+AC_CHECK_FUNCS([getopt getopt_long])
 
 dnl Overall configuration success flag
 oggz_config_ok=yes
@@ -352,20 +340,20 @@
 # MacOS 10.4 only declares timezone for _XOPEN_SOURCE. Check for this.
 AC_MSG_CHECKING([for timezone])
 AC_COMPILE_IFELSE(
-  AC_LANG_PROGRAM([#include <time.h>], [
+  [AC_LANG_PROGRAM([[#include <time.h>]], [[
     int seconds = 1234567;
     seconds -= timezone;
-  ]), AC_MSG_RESULT([ok]),
+  ]])], AC_MSG_RESULT([ok]),
   AC_MSG_RESULT([no])
   AC_MSG_CHECKING([for timezone with _XOPEN_SOURCE])
   AC_COMPILE_IFELSE(
-    AC_LANG_PROGRAM([
+    [AC_LANG_PROGRAM([[
 #define _XOPEN_SOURCE
 #include <time.h>
-    ], [
+    ]], [[
       int seconds = 1234567;
       seconds -= timezone;
-    ]),
+    ]])],
    dnl _XOPEN_SOURCE is required
     AC_MSG_RESULT([ok])
     AC_DEFINE([_XOPEN_SOURCE], [1], 
@@ -379,7 +367,7 @@
 
 dnl Add some useful warnings if we have gcc.
 dnl changequote(,)dnl
-if test "x$ac_cv_prog_gcc" = xyes ; then
+if test "x$ac_cv_c_compiler_gnu" = xyes ; then
   CFLAGS="$CFLAGS -Wall -Wextra -g -std=gnu99 -Wdeclaration-after-statement -Wno-unused"
 fi
 dnl changequote([,])dnl
@@ -473,7 +461,7 @@
 AC_SUBST(SHLIB_VERSION_ARG)
 AC_SUBST(SHARED_VERSION_INFO)
 
-AC_OUTPUT([
+AC_CONFIG_FILES([
 Makefile
 doc/Makefile
 doc/Doxyfile
@@ -492,6 +480,7 @@
 oggz.pc
 oggz-uninstalled.pc
 ])
+AC_OUTPUT
 
 AC_MSG_RESULT([
 ------------------------------------------------------------------------
diff -urN liboggz-1.0.2-orig/Makefile.am liboggz-1.0.2/Makefile.am
--- liboggz-1.0.2-orig/Makefile.am	2010-02-11 16:35:05.000000000 +0800
+++ liboggz-1.0.2/Makefile.am	2025-11-14 11:13:56.815657600 +0800
@@ -11,7 +11,7 @@
 #  SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
  
 ## Process this file with automake to produce Makefile.in
-
+ACLOCAL_AMFLAGS = -I m4
 DISTCHECK_CONFIGURE_FLAGS = --enable-gcc-werror
 
 SUBDIRS = doc include src
diff -urN liboggz-1.0.2-orig/src/examples/Makefile.am liboggz-1.0.2/src/examples/Makefile.am
--- liboggz-1.0.2-orig/src/examples/Makefile.am	2010-01-30 13:55:59.000000000 +0800
+++ liboggz-1.0.2/src/examples/Makefile.am	2025-11-14 11:13:56.817652700 +0800
@@ -1,6 +1,6 @@
 ## Process this file with automake to produce Makefile.in
 
-INCLUDES = -I$(top_builddir) -I$(top_builddir)/include \
+AM_CPPFLAGS = -I$(top_builddir) -I$(top_builddir)/include \
            -I$(top_srcdir)/include \
            @OGG_CFLAGS@
 
diff -urN liboggz-1.0.2-orig/src/liboggz/Makefile.am liboggz-1.0.2/src/liboggz/Makefile.am
--- liboggz-1.0.2-orig/src/liboggz/Makefile.am	2010-01-30 13:55:59.000000000 +0800
+++ liboggz-1.0.2/src/liboggz/Makefile.am	2025-11-14 11:13:56.819235000 +0800
@@ -1,6 +1,6 @@
 ## Process this file with automake to produce Makefile.in
 
-INCLUDES = $(INCLTDL) \
+AM_CPPFLAGS = $(INCLTDL) \
            -I$(top_builddir) -I$(top_builddir)/include \
            -I$(top_srcdir)/include \
            -I$(top_srcdir)/src/liboggz \
@@ -27,4 +27,4 @@
 	dirac.c dirac.h
 
 liboggz_la_LDFLAGS = -version-info @SHARED_VERSION_INFO@ @SHLIB_VERSION_ARG@
-liboggz_la_LIBADD = @OGG_LIBS@
+liboggz_la_LIBADD = @OGG_LIBS@ @LIBS@
diff -urN liboggz-1.0.2-orig/src/tests/Makefile.am liboggz-1.0.2/src/tests/Makefile.am
--- liboggz-1.0.2-orig/src/tests/Makefile.am	2010-04-29 08:36:42.000000000 +0800
+++ liboggz-1.0.2/src/tests/Makefile.am	2025-11-14 11:13:56.821232000 +0800
@@ -1,6 +1,6 @@
 ## Process this file with automake to produce Makefile.in
 
-INCLUDES = -I$(top_builddir) -I$(top_builddir)/include \
+AM_CPPFLAGS = -I$(top_builddir) -I$(top_builddir)/include \
            -I$(top_srcdir)/include \
            @OGG_CFLAGS@
 
diff -urN liboggz-1.0.2-orig/src/tools/Makefile.am liboggz-1.0.2/src/tools/Makefile.am
--- liboggz-1.0.2-orig/src/tools/Makefile.am	2010-01-30 13:55:59.000000000 +0800
+++ liboggz-1.0.2/src/tools/Makefile.am	2025-11-14 11:13:56.823263200 +0800
@@ -6,7 +6,7 @@
 
 bin_SCRIPTS = oggz-diff
 
-INCLUDES = -I$(top_builddir) -I$(top_builddir)/include \
+AM_CPPFLAGS = -I$(top_builddir) -I$(top_builddir)/include \
            -I$(top_srcdir)/include \
            -I$(top_srcdir)/src/liboggz \
            @OGG_CFLAGS@
@@ -42,7 +42,7 @@
 oggz_known_codecs_LDADD = $(OGGZ_LIBS)
 
 oggz_info_SOURCES = oggz-info.c skeleton.c $(COMMON_SRCS)
-oggz_info_LDADD = $(OGGZ_LIBS) -lm
+oggz_info_LDADD = $(OGGZ_LIBS)
 
 oggz_comment_SOURCES = oggz-comment.c $(COMMON_SRCS)
 oggz_comment_LDADD = $(OGGZ_LIBS)
diff -urN liboggz-1.0.2-orig/src/tools/oggz-chop/Makefile.am liboggz-1.0.2/src/tools/oggz-chop/Makefile.am
--- liboggz-1.0.2-orig/src/tools/oggz-chop/Makefile.am	2010-01-30 13:55:59.000000000 +0800
+++ liboggz-1.0.2/src/tools/oggz-chop/Makefile.am	2025-11-14 11:13:56.825221500 +0800
@@ -1,6 +1,6 @@
 ## Process this file with automake to produce Makefile.in
 
-INCLUDES = -I$(top_builddir) -I$(top_builddir)/include \
+AM_CPPFLAGS = -I$(top_builddir) -I$(top_builddir)/include \
            -I$(top_srcdir)/include -I$(top_srcdir)/src/tools \
            -I$(top_srcdir)/src/liboggz \
            -I$(top_srcdir)/src/tests \
@@ -28,7 +28,7 @@
 
 oggz_chop_SOURCES = oggz-chop.c $(srcdir)/../oggz_tools.c $(srcdir)/../skeleton.c $(srcdir)/../mimetypes.c \
                     $(srcdir)/../../liboggz/dirac.c cmd.c cgi.c header.c httpdate.c main.c timespec.c
-oggz_chop_LDADD = $(OGGZ_LIBS) -lm
+oggz_chop_LDADD = $(OGGZ_LIBS)
 
 httpdate_test_SOURCES = httpdate.c httpdate_test.c
 
